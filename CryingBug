int fileN = 0;  // имя файла скриншота
int dst = 50;   // дистанция крика в пикселях

int count = 10;  // максимальное количество букашек
int rcount= 2;     // количество ресурсов

Antbot[] ants = new Antbot[count] ;  // массив букашек

Resource[] food = new Resource[rcount] ; //массив ресурсов

int cA = 0; //индекс текущей букашки

int antCount; // счётчик количества букашек

//================================================================================
void setup()
{
  size(1800,900);  ellipseMode(CENTER);
  background(0);  smooth();
  
  // инициализируем всех букашек, но пока они мертвы
  for( int i=0; i<count; i++ )  { ants[i]= new Antbot(i); }
  
    // Всё начинается с букашки под индексом 0
    // Она всегда будет виртуальной(мёртвой)
    // Она ссылается на первую живую букашку
    // а последняя живая букашка ссылается на неё
    // сейчас она ссылается на единственную живую букашку с индексом 4
  ants[0].setbot(0, 4, 4,   0,   0, 0 );
    // а букашка под индексем 4 - это королева
  ants[4].setbot(6, 0, 0, 900, 400, 0 );
    // и у неё в запасе есть ресурсы, для производства начального роя
  ants[4].foodR=1000; ants[4].foodB=1000;
    // создаём ресурсы
  for( int i=0; i<rcount; i++ )  { food[i]= new Resource(); }
}

//==============================================================================
void draw()
{
  background(0);  noStroke();
  
    // ресурсы
  for( int i=0; i<rcount; i++ ) {
    food[i].step(); food[i].render(); 
  }  

    // начинаем с букашки, на которую ссылается букашка с индексом 0  
  cA = ants[0].next;
    // пока не придём к букашке с индексом 0 
  while(cA != 0 ){ 
    ants[cA].step(); 
    cA = ants[cA].next; 
  }
  
    // обнуляем счётчик количества букашек
  antCount=0;  
    // а теперь отрисовываем всех букашек
  cA = ants[0].next;
  while(cA != 0 ){ 
    ants[cA].render(); 
    cA = ants[cA].next; 
    antCount++;
  }  

    //---выводим инфо --------
  fill(255);  text(frameCount, 30,20);  text("ants "+antCount, 50,40);
  fill(200); text(fileN, 90,20);

}
